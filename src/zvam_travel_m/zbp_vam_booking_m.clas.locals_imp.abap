CLASS lhc_booking DEFINITION INHERITING FROM cl_abap_behavior_handler.

  PRIVATE SECTION.

    METHODS earlynumbering_cba_Bookingsupp FOR NUMBERING
      IMPORTING entities FOR CREATE Booking\_Bookingsuppl.

ENDCLASS.

CLASS lhc_booking IMPLEMENTATION.

  METHOD earlynumbering_cba_Bookingsupp.

  DATA : max_booking_suppl_id TYPE /dmo/booking_supplement_id.

  READ ENTITIES OF zi_vam_travel_m IN LOCAL MODE
       ENTITY booking by \_BookingSuppl
       FROM CORRESPONDING #( entities )
       LINK DATA(booking_supplements).

  "Loop over all %tky-transaction key (BookingId+TravelId)

  LOOP AT entities ASSIGNING FIELD-SYMBOL(<booking_group>)
                             GROUP BY <booking_group>-%tky.

      "Get max booking_suppl_id from booking_suppliments belonging to the booking.
      max_booking_suppl_id = REDUCE #( INIT max = CONV /dmo/booking_supplement_id( '0' )

                                    FOR booksuppl IN booking_supplements USING KEY entity
                                                  WHERE (     source-TravelId = <booking_group>-TravelId
                                                          AND source-BookingId = <booking_group>-BookingId )

                                     NEXT max = COND /dmo/booking_supplement_id( WHEN booksuppl-target-BookingSupplementId > max
                                                                                 THEN booksuppl-target-BookingSupplementId
                                                                                 ELSE max )

                                   ).

      "Get highest booking_suppl_id from incoming booking_suppliments.

      max_booking_suppl_id = REDUCE #( INIT max = max_booking_suppl_id

                                       FOR entity IN entities USING KEY entity
                                                  WHERE (     TravelId = <booking_group>-TravelId
                                                          AND BookingId = <booking_group>-BookingId )

                                       FOR target in entity-%target

                                       NEXT max = COND /dmo/booking_supplement_id( WHEN target-BookingSupplementId > max
                                                                                 THEN target-BookingSupplementId
                                                                                 ELSE max )

                                      ).

      "Loop over all entries in entities with the same TravelId and BookingId
      LOOP AT entities ASSIGNING FIELD-SYMBOL(<booking>) USING KEY entity WHERE TravelId = <booking_group>-TravelId
                                                                          AND   BookingId = <booking_group>-BookingId.

              "Assign new booking supplement-Id's
              LOOP AT <booking>-%target ASSIGNING FIELD-SYMBOL(<booksuppl_wo_numbers>).

              APPEND CORRESPONDING #( <booksuppl_wo_numbers> ) TO MAPPED-booksuppl ASSIGNING FIELD-SYMBOL(<mapped_booksuppl>).

              IF <booksuppl_wo_numbers>-BookingSupplementId IS INITIAL.


                    max_booking_suppl_id += 1.
                    <mapped_booksuppl>-BookingSupplementId = max_booking_suppl_id.



              ENDIF.

              ENDLOOP.


      ENDLOOP.

  ENDLOOP.


  ENDMETHOD.

ENDCLASS.

*"* use this source file for the definition and implementation of
*"* local helper classes, interface definitions and type
*"* declarations
